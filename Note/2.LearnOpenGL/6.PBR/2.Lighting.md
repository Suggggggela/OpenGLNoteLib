
上节我们讲解了PBR的基础知识，这节我们将这些知识应用在实际的Renderer中

//这一节我们只考虑**直接光照**（这也是为什么下边对于点p处认为除了光源方向ωi以外均认为贡献值为0）
//对于间接光照，我们会在IBL部分考虑

## 1.获取光源能量
对于着色来说，我们的单位是单个像素，那么单位面积就是前提了
那么在单位面积下，首先要考虑的就是**入射的所有Radiance或者说Irradiance如何获取**

在这个情况下，对于着色点p：
- Radiance衡量的是单位立体角ω方向的Radiant Flux
- Irradiance衡量的是当前所有的Radiance

因为ωi足够微小，我们不妨假设它无限小
此时Radiance就等同于光源在单一光线/方向上的radiant flux

举个例子说：
            假设我们有一个点光源，点光源被认为向所有方向均匀的辐射能量
            它的radiant flux是（23.47, 21.31, 20.79）
            它的Radiant Intensity就是光源往外辐射所有方向的Radiant Flux
            在下边的场景中只有一个点光源，那么对于点p，所有可能入射过来的方向中只有ωi是直接来自光源的
            这种情况下如果我们只考虑**直接光照**，点p的其他入可能入射方向观测到的Radiance均认为是0
            ![[assets/2.Lighting_image_1.png]]
        如果我们先不考虑光随距离的衰减，也先不考虑入射角θ对能量的Scale：
            前边已经假设点光源均匀的向外辐射，那么无论从哪个方向看，光源入射进来(或者说光源辐射出去)光线的Radiant Intensity都是一样的
            此时光源的Radiant Intensity可以近似为它的Radiant Flux，也就是上边的（23.47, 21.31, 20.79）

然而，Radiance以点p的位置为输入，并且实际上点光源都会考虑衰减
            所以实际中，对点p做shading时，我们认为点光源的Radiant Intensity（辐射出的flux）根据点p到光源的距离进行衰减
        
再考虑入射角θ对能量的Scale
            我们会像反射方程中那样，根据n·ωi对能量进行二次Scale

总的来讲，对于**直接光照**，入射到点p的单一光线ωi（从点p到light的方向向量），点光源的Radiance（L）的计算，我们需要考虑：
- light color（光源中定义）
- 随距离衰减（后续会讲）
- 根据θ进行Scale（同反射方程中描述）

代码如下：
```cpp
vec3 lightColor = vec3(23.47, 21.31, 20.79); 
vec3 wi = normalize(lightPos - fragPos); 
float cosTheta = max(dot(N, Wi), 0.0); 
float attenuation = calculateAttenuation(fragPos, lightPos); 
vec3 radiance = lightColor * attenuation * cosTheta;
```

以上就是计算点光源Radiance的方法，对于其他光源我们可以类比，比如:
- directional light，定向/平行光：拥有恒定的ωi，不会有衰减
- spotlight，聚光源：没有恒定的Radiant Intensity，通过聚光源的forward direction vector进行Scale


再来说半球Ω上的积分
            由于对于表面上的单个点pShading时，已经事先知道所有光源的具体位置，我们就不需要求解积分了
            我们可以直接取已知光源的数量，来计算它们带来的总Irradiance（for循环累加）
            由于对于点p，每个光源只有一个光源方向会影响表面的Radiance，这就使得**直接光照**对于PBR来说相对简单，直接遍历所有光源即可
            后续考虑**间接光照**时（IBL），对于环境光，由于点p处可能受光的所有方向都要考虑，那时候就需要积分了
```
//注：不太明白的可以回顾一下辐射度量学的内容，我们简单梳理一下：
-Radiant Energy：辐射的能量
-Radiant Flux/Power：单位时间内辐射的能量（后续概念都默认是单位时间）
-Radiant Intensity：单位立体角通过的Flux，用于描述光源往外辐射能量
-Irradiance：单位面积通过的Flux，用于描述表面接收能量
-Radiance：单位面积单位立体角通过的Flux，用于描述光线传播的过程

描述光源辐射能量，我们一般用前三个
描述表面接收能量，我们一般用Irradiance
Radiance用于描述光线传播的过程，所以你想怎么用都行

```


## 2. A PBR Surface Model
接下来我们动手编写一个PBR Shader
### 输入
首先是需要的数据，我们通过VS和Uniform传入FS
```cpp
#version 330 core
out vec4 FragColor;
in vec2 TexCoords;
in vec3 WorldPos;
in vec3 Normal;

uniform vec3 camPos;

uniform vec3  albedo;
uniform float metallic;
uniform float roughness;
uniform float ao;

void main()
{
    vec3 N = normalize(Normal); 
    vec3 V = normalize(camPos - WorldPos);
    ...
}
```


### 直接光照
本节的例子中场景有4个点光源，它们共同构成场景的Irradiance
对每个点p做Shading就是对点p应用反射方程：
            我们遍历每个光源计算其独立的Radiance
            然后应用BRDF和θ对Radiance的Scale贡献
            最后将结果求和，直到求解出直接光照
            
首先是每个光源的变量：
```cpp
vec3 Lo = vec3(0.0);
for(int i = 0; i < 4; ++i) 
{
    vec3 L = normalize(lightPositions[i] - WorldPos);
    vec3 H = normalize(V + L);
  
    float distance    = length(lightPositions[i] - WorldPos);
    float attenuation = 1.0 / (distance * distance);
    vec3 radiance     = lightColors[i] * attenuation; 
    ...
}

//因为默认在线性空间计算，我们使用更为物理正确的distance平方作为衰减计算
```

然后对于每个光源都计算完整的Cook-Torrance specular BRDF项
![[assets/2.Lighting_image_2.png]]

**Fresnel Term**
首先要计算的是镜面反射（反射）和漫反射（折射）的比例，它可以通过Fresnel项计算：
```cpp
vec3 fresnelSchlick(float cosTheta, vec3 F0) 
{ 
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0); //clamp是为了避免黑点
}
vec3 F0 = vec3(0.04); 
F0 = mix(F0, albedo, metallic); 
vec3 F = fresnelSchlick(max(dot(H, V), 0.0), F0);
```
前边理论部分我们已经谈到金属和非金属的菲涅尔项如何处理，它需要一个F0值，这个F0代表从表面法线方向观察的基础反射率
- 对于非金属，0.04作为F0已经足够好，基础反射率不会超过0.17，它srgb形式基本没有颜色倾向
- 对于金属，我们使用Metallic对0.04和Albedo进行插值，基础反射率介于0.5~1.0，其srgb形式大多有颜色倾向
![[assets/2.Lighting_image_3.png|0x0]]

对于直接光照，剩余项所需的计算方法如下

```cpp
//注：
    //直接将粗糙度传入进行计算
    //根据迪士尼的观察结果，对粗糙度进行平方后，光照效果更符合预期(UE也是这么写的)

//D Term
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a      = roughness*roughness;
    float a2     = a*a;
    float NdotH  = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;
	
    float num   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;
	
    return num / denom;
}

//G Term
float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float num   = NdotV;
    float denom = NdotV * (1.0 - k) + k;
	
    return num / denom;
}
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2  = GeometrySchlickGGX(NdotV, roughness);
    float ggx1  = GeometrySchlickGGX(NdotL, roughness);
	
    return ggx1 * ggx2;
}


//Calculate BRDF
float NDF = DistributionGGX(N, H, roughness); 
float G = GeometrySmith(N, V, L, roughness);
vec3 numerator = NDF * G * F; 
float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.0001;
//+0.0001是防止出现点积结果为0后作为分母的情况
vec3 specular = numerator / denominator;

vec3 kS = F;//菲涅尔项等于反射贡献
vec3 kD = vec3(1.0) - kS; //根据反射折射互斥，计算出漫反射（折射）
kD *= 1.0 - metallic;//金属表面不发生折射，所以没有漫反射

//Calculate ReflectionEquation
const float PI = 3.14159265359;
float NdotL = max(dot(N, L), 0.0);
Lo += (kD * albedo / PI + specular) * radiance * NdotL;
//上边我们说过，对于直接光照最后的Lo我们根据光源数量进行累加即可

//最后加上简单的环境光
vec3 ambient = vec3(0.03) * albedo * ao; 
vec3 color = ambient + Lo;
```

### 线性空间和HDR
PBR的输入均为线性数据，计算是在线性空间中进行的
除此之外，我们希望光照输入值接近物理对应值，以满足Irradiance和色彩纸在更宽广的范围内大幅波动
所以在Gamma校正前我们我们会将转换为HDR，然后再通过ToneMapping转换为LDR

```cpp
//这里没有额外的PostProcess Pass
//ToneMapping直接在Shader中进行计算
color = color / (color + vec3(1.0)); //Reinhard ToneMapping
color = pow(color, vec3(1.0/2.2)); // Gamma校正
```

在PBR流程中，在线性空间计算和HDR非常重要，如果没有这两部分，最终的结果将无法准确的捕捉不同光照强度下高光和暗部的细节，从而导致计算结果失真，呈现出的画面欠佳：
![[assets/2.Lighting_image_4.png|642x341]]

### 直接光照Shader

```cpp
#version 330 core
out vec4 FragColor;
in vec2 TexCoords;
in vec3 WorldPos;
in vec3 Normal;

// material parameters
uniform vec3  albedo;
uniform float metallic;
uniform float roughness;
uniform float ao;

// lights
uniform vec3 lightPositions[4];
uniform vec3 lightColors[4];

uniform vec3 camPos;

const float PI = 3.14159265359;
  
float DistributionGGX(vec3 N, vec3 H, float roughness);
float GeometrySchlickGGX(float NdotV, float roughness);
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness);
vec3 fresnelSchlick(float cosTheta, vec3 F0);

void main()
{		
    vec3 N = normalize(Normal);
    vec3 V = normalize(camPos - WorldPos);

    vec3 F0 = vec3(0.04); 
    F0 = mix(F0, albedo, metallic);
	           
    // reflectance equation
    vec3 Lo = vec3(0.0);
    for(int i = 0; i < 4; ++i) 
    {
        // calculate per-light radiance
        vec3 L = normalize(lightPositions[i] - WorldPos);
        vec3 H = normalize(V + L);
        float distance    = length(lightPositions[i] - WorldPos);
        float attenuation = 1.0 / (distance * distance);
        vec3 radiance     = lightColors[i] * attenuation;        
        
        // cook-torrance brdf
        float NDF = DistributionGGX(N, H, roughness);        
        float G   = GeometrySmith(N, V, L, roughness);      
        vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);       
        
        vec3 kS = F;
        vec3 kD = vec3(1.0) - kS;
        kD *= 1.0 - metallic;	  
        
        vec3 numerator    = NDF * G * F;
        float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.0001;
        vec3 specular     = numerator / denominator;  
            
        // add to outgoing radiance Lo
        float NdotL = max(dot(N, L), 0.0);                
        Lo += (kD * albedo / PI + specular) * radiance * NdotL; 
    }   
  
    vec3 ambient = vec3(0.03) * albedo * ao;
    vec3 color = ambient + Lo;
	
    color = color / (color + vec3(1.0));
    color = pow(color, vec3(1.0/2.2));  
   
    FragColor = vec4(color, 1.0);
}  
```

我们渲染7x7个球体分别给不同的粗糙度和金属度，效果如下：
- 从下到上金属度为0~1
- 从左到右粗糙度为0~1
![[assets/2.Lighting_image_5.png|736x419]]

## 3.Textured PBR
如果你接触过PBR流程，你可能回想：PBR的那一套贴图去哪了
实际上我们可以将所有计算反射方程的数据通过贴图输入进来，这也符合我们想做的事情（通过贴图控制不同部分的材质参数）

你可以下载本节使用的PBR贴图 ： https://freepbr.com/product/rusted-iron-pbr-metal-material-alt/

这里FS的TBN是通过DDXDDY计算的切线
```cpp
#version 330 core
out vec4 FragColor;
in vec2 TexCoords;
in vec3 WorldPos;
in vec3 Normal;

// material parameters
uniform sampler2D albedoMap;
uniform sampler2D normalMap;
uniform sampler2D metallicMap;
uniform sampler2D roughnessMap;
uniform sampler2D aoMap;

// lights
uniform vec3 lightPositions[4];
uniform vec3 lightColors[4];

uniform vec3 camPos;

const float PI = 3.14159265359;
// ----------------------------------------------------------------------------
// Easy trick to get tangent-normals to world-space to keep PBR code simplified.
// Don't worry if you don't get what's going on; you generally want to do normal 
// mapping the usual way for performance anyways; I do plan make a note of this 
// technique somewhere later in the normal mapping tutorial.
vec3 getNormalFromMap()
{
    vec3 tangentNormal = texture(normalMap, TexCoords).xyz * 2.0 - 1.0;

    vec3 Q1  = dFdx(WorldPos);
    vec3 Q2  = dFdy(WorldPos);
    vec2 st1 = dFdx(TexCoords);
    vec2 st2 = dFdy(TexCoords);

    vec3 N   = normalize(Normal);
    vec3 T  = normalize(Q1*st2.t - Q2*st1.t);
    vec3 B  = -normalize(cross(N, T));
    mat3 TBN = mat3(T, B, N);

    return normalize(TBN * tangentNormal);
}
// ----------------------------------------------------------------------------
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness*roughness;
    float a2 = a*a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}
// ----------------------------------------------------------------------------
float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}
// ----------------------------------------------------------------------------
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}
// ----------------------------------------------------------------------------
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}
// ----------------------------------------------------------------------------
void main()
{		
    //Albeo贴图为sRGB贴图，记得自己转到线性空间
    //粗糙度和金属度总是在线性空间中创建的
    //有时AO也需要从sRGB转换到线性空间，这个视情况而定
    //Unity等引擎中在Texture设置处可以自行设置贴图是否为sRGB，它会自动为你转换
    vec3 albedo     = pow(texture(albedoMap, TexCoords).rgb, vec3(2.2));
    float metallic  = texture(metallicMap, TexCoords).r;
    float roughness = texture(roughnessMap, TexCoords).r;
    float ao        = texture(aoMap, TexCoords).r;

    vec3 N = getNormalFromMap();
    vec3 V = normalize(camPos - WorldPos);

    // calculate reflectance at normal incidence; if dia-electric (like plastic) use F0 
    // of 0.04 and if it's a metal, use the albedo color as F0 (metallic workflow)    
    vec3 F0 = vec3(0.04); 
    F0 = mix(F0, albedo, metallic);

    // reflectance equation
    vec3 Lo = vec3(0.0);
    for(int i = 0; i < 4; ++i) 
    {
        // calculate per-light radiance
        vec3 L = normalize(lightPositions[i] - WorldPos);
        vec3 H = normalize(V + L);
        float distance = length(lightPositions[i] - WorldPos);
        float attenuation = 1.0 / (distance * distance);
        vec3 radiance = lightColors[i] * attenuation;

        // Cook-Torrance BRDF
        float NDF = DistributionGGX(N, H, roughness);   
        float G   = GeometrySmith(N, V, L, roughness);      
        vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);
           
        vec3 numerator    = NDF * G * F; 
        float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.0001; // + 0.0001 to prevent divide by zero
        vec3 specular = numerator / denominator;
        
        // kS is equal to Fresnel
        vec3 kS = F;
        // for energy conservation, the diffuse and specular light can't
        // be above 1.0 (unless the surface emits light); to preserve this
        // relationship the diffuse component (kD) should equal 1.0 - kS.
        vec3 kD = vec3(1.0) - kS;
        // multiply kD by the inverse metalness such that only non-metals 
        // have diffuse lighting, or a linear blend if partly metal (pure metals
        // have no diffuse light).
        kD *= 1.0 - metallic;	  

        // scale light by NdotL
        float NdotL = max(dot(N, L), 0.0);        

        // add to outgoing radiance Lo
        Lo += (kD * albedo / PI + specular) * radiance * NdotL;  // note that we already multiplied the BRDF by the Fresnel (kS) so we won't multiply by kS again
    }   
    
    // ambient lighting (note that the next IBL tutorial will replace 
    // this ambient lighting with environment lighting).
    vec3 ambient = vec3(0.03) * albedo * ao;
    
    vec3 color = ambient + Lo;

    // HDR tonemapping
    color = color / (color + vec3(1.0));
    // gamma correct
    color = pow(color, vec3(1.0/2.2)); 

    FragColor = vec4(color, 1.0);
}

```
